PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX aas: <https://admin-shell.io/aas/3/0/RC01/>
PREFIX aasref: <https://admin-shell.io/aas/3/0/RC01/Reference/>
PREFIX aaskey: <https://admin-shell.io/aas/3/0/RC01/Key/>
PREFIX aasrel: <https://admin-shell.io/aas/3/0/RC01/RelationshipElement/>
PREFIX aaskeyelem: <https://admin-shell.io/aas/3/0/RC01/KeyElements/>

DELETE {
    ?rel aasrel:first ?ref .
    ?ref a aas:Reference ;
        aasref:key ?key ; 
        a owl:NamedIndividual . 
    ?key a aas:Key ;
        aaskey:value ?val ;
        aaskey:type ?type ;
        a owl:NamedIndividual . 
}
INSERT {
    aasrel:firstValue a owl:DatatypeProperty . 
    ?rel aasrel:firstValue ?val . 
}
WHERE {
    ?rel a aas:RelationshipElement ; 
        aasrel:first ?ref .
    ?ref a aas:Reference ; 
        aasref:key ?key . 
    ?key a aas:Key ;
        aaskey:value ?val ;
        aaskey:type aaskeyelem:GLOBALREFERENCE .
}
